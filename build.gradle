import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'groovy'
    id 'java'
    id 'application' // requires for running application
    id 'checkstyle'
    id 'com.bmuschko.docker-remote-api' version '9.1.0'
}

group 'com.github.mikesukim'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    // Logging
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.0'
    implementation group: 'org.slf4j', name: 'slf4j-log4j12', version: '2.0.0'

    // Jersey2-Guice
    // https://github.com/logzio/guice-jersey
    // When updating this version, all jersey related libraries should be updated as well.
    // e.x jersey-media-json-jackson, jersey-bean-validation
    implementation 'io.logz:guice-jersey:1.0.13'

    // Bean validation (both uses Hibernate validator)
    // version 3 cannot be used since not compatible with Jersey-2.
    implementation 'org.glassfish.jersey.ext:jersey-bean-validation:2.37' // To be used with Jersey class resource
    implementation 'ru.vyarus:guice-validator:2.0.1' // To be used with Guice classes

    // AWS SDK
    implementation platform('software.amazon.awssdk:bom:2.17.0')
    implementation 'software.amazon.awssdk:dynamodb'
    implementation 'software.amazon.awssdk:dynamodb-enhanced'

    // Jackson
    // https://github.com/FasterXML/jackson
    implementation 'org.glassfish.jersey.media:jersey-media-json-jackson:2.37'

    // Immutables
    // http://immutables.github.io
    def immutablesVersion = "2.9.2"
    // annotationProcessor is required for Gradle to process annotation processing
    // https://stackoverflow.com/questions/47351968/how-to-make-githubs-immutables-work-in-intellij-gradle/59917737#59917737
    annotationProcessor "org.immutables:value:$immutablesVersion"
    implementation "org.immutables:value:$immutablesVersion"

    // JJWT
    // https://github.com/jwtk/jjwt
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5',
            // Uncomment the next line if you want to use RSASSA-PSS (PS256, PS384, PS512) algorithms:
            //'org.bouncycastle:bcprov-jdk15on:1.70',
            'io.jsonwebtoken:jjwt-jackson:0.11.5' // or 'io.jsonwebtoken:jjwt-gson:0.11.5' for gson

    // Groovy
    testImplementation 'org.codehaus.groovy:groovy-all:3.0.13'

    // Spock Testing
    testImplementation 'org.spockframework:spock-core:2.3-groovy-3.0'
    testImplementation group: 'cglib', name: 'cglib-nodep', version: '2.2'
    testImplementation 'org.objenesis:objenesis:3.0.1'
}

test {
    useJUnitPlatform()
}

tasks.register('create-jar') {
    dependsOn tasks.clean
    dependsOn tasks.build
    dependsOn tasks.check
    dependsOn tasks.test
    dependsOn tasks.jar

    // When multiple dependsOn, its order is only guaranteed with mustRunAfter
    tasks.findByName('build').mustRunAfter 'clean'
    tasks.findByName('jar').mustRunAfter 'test'
}

tasks.register('run-server', JavaExec){
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'chorestogetherapiservice.ChoresTogetherAPIService'
}

tasks.register('run-docker-server', Exec){
    if (Os.isFamily(Os.FAMILY_MAC)) {
        commandLine 'sh', './docker-server-run.sh'
    }
}

// Checkstyle version needed to be specified to be compatible with Google's checkstyle
// Google checkstyle rules :
// https://checkstyle.org/styleguides/google-java-style-20180523/javaguide.html#s7.1-javadoc-formatting
checkstyle {
    toolVersion '10.5.0'
}

// To make jar executable, specifying manifest is required.
jar {
    manifest {
        attributes(
                'Main-Class': 'chorestogetherapiservice.ChoresTogetherAPIService',
                'Class-Path': configurations.runtimeClasspath.files { it.getName() }.join(' ')
        )
    }
}